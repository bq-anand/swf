#!/usr/bin/env coffee

path = require "path"
parse = require "path-parse"
yargs = require "yargs"
domain = require "domain"
Worker = require "../lib/Actor/Worker"
createLogger = require "../core/helper/logger"
createKnex = require "../core/helper/knex"
createBookshelf = require "../core/helper/bookshelf"
createMongoDB = require "../core/helper/mongodb"
createSWF = require "../core/helper/swf"


argv = yargs
  .usage('Usage: $0 [options] activityTaskPath')
  .options(
    "s":
      alias: "settings"
      type: "string"
      description: "Settings for dependencies (SWF binding, logger, etc)"
      demand: true
    "d":
      alias: "domain"
      type: "string"
      description: "Amazon SWF domain"
      demand: true
    "t":
      alias: "task-list-name"
      type: "string"
      description: "Amazon SWF taskList name (guessed from activityTaskPath by default)"
      demand: false
      default: null
    "i":
      alias: "identity"
      type: "string"
      description: "Amazon SWF identity (defaults to \"worker://{{hostname}}/{{task-list-name}}\#{{pid}}\")"
      demand: false
    "m":
      alias: "max-loops"
      type: "number"
      description: "Execute that many loops and exit (useful for testing)"
      demand: false
      default: 0
    "u":
      alias: "timeout"
      type: "number"
      description: "Override SWF socket timeout (useful for testing) (in milliseconds)"
      demand: false
      default: 0
  )
  .demand(1)
  .strict()
  .argv

settings = require path.resolve(process.cwd(), argv.settings)
taskPath = argv._[0]
taskListName = argv.taskListName or parse(taskPath).name
taskCls = require path.resolve(process.cwd(), taskPath)
identity = argv.identity or "worker://#{require("os").hostname()}/#{taskListName}\##{process.pid}"

if argv.timeout
  settings.swf.httpOptions ?= {}
  settings.swf.httpOptions.timeout = argv.timeout

logger = createLogger(settings.logger)
knex = createKnex(settings.knex)
bookshelf = createBookshelf(knex)
swf = createSWF(settings.swf)
mongodb = createMongoDB(settings.mongodb)

worker = new Worker
  domain: argv.domain
  taskList:
    name: taskListName
  identity: identity
  taskCls: taskCls
  maxLoops: argv.maxLoops
,
  logger: logger
  swf: swf
  bookshelf: bookshelf
  knex: knex
  mongodb: mongodb

worker.start("vodka") # hope it helps

(require "death")(-> process.exit(0)) # In nomine SIGINT, SIGTERM, SIGQUIT, amen